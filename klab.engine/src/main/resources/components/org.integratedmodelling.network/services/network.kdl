/**
 * This file defines the contract for the services in k.IM that create and operate on networks.
 */
@klab 0.10.0
@version 0.10.0
@namespace klab.network

export object connect
	"Connects subjects by building relationships using configurable deterministic or stochastic methods. Each 
	 relationship can optionally be assigned a spatial context connecting the space of the related subjects 
	 with configurable methods."
{		
	optional number p
		"The parameter that defines the network being created. Its meaning changes according to the method. If the
		selector expression is passed, this defaults to 1.0."	
		
	optional boolean expression select
		"An expression evaluated per each legitimate pair of objects to be connected, returning true or false. If
		 false is returned, the relationship will not be created. The variables 'source' and 'target' are set to
		 the objects to be linked."
	    default ""
		
	optional import object source
		"Artifact providing the source for the relationships. By default all artifacts that incarnate the 
		 relationship source type are used."

	optional import object target
		"Artifact providing the target for the relationships. By default all artifacts that incarnate the 
		 relationship target type are used."

	optional enum space
		"Type of spatial link to create if the vertices are spatial and distinct.
	     Possible values are None, Line, LineCentroid, LineEdge, ConvexHull. Default is ConvexHull
	     if the source and target are polygonal, Line if at least one is a point, and None if they are 
	     non-spatial."
	     values None, Line, LineCentroid, LineEdge, ConvexHull
	     
	optional number seed
		"The random number seed. By default each generation will use a different one."     
	     
	optional boolean selfconnections
		"If true, self-connections can be created. Default is false"
		default false

	optional boolean reciprocal
		"If true, A->B does not prevent B->A from being created. Default false. Forced to false
		 if the annotated concept is a bond."

	optional enum method 
		"The method to use to create the random network. Choose between OutDegree, BarabasiAlbert, EppsteinPowerLaw, 
		 ErdosRenyi, Lattice2D and KleinbergSmallWorld (the last two restricted to grid space contexts). For non-random 
		 networks, Closest will work in spatial contexts to connect the two spatially closest objects.
		 ## TODO Markdown documentation
		" 
		values OutDegree, BarabasiAlbert, EppsteinPowerLaw, ErdosRenyi, KleinbergSmallWorld, Closest
		default "ErdosRenyi"
		
	class org.integratedmodelling.klab.components.network.services.ConfigurableRelationshipInstantiator

}

export object route 
	"Connects subjects through routes computed along a specified spatial configuration. Relationships are only
	 created if a route exists."
{
	optional import object source
		"Semantics for the type of the subjects used as source for the relationships"
		default ""

	optional import object target
		"Semantics for the type of the subjects used as target for the relationships"
		default ""
		
	optional number time_limit
		"Time threshold to consider a route a valid option in seconds"
		default "No limit"
		
	optional number distance_limit
		"Distance threshold to consider a route a valid option in kilometers"
		default "No limit"
	
	optional enum transport
		"Type of transport used to find a route"
		values Auto, Pedestrian, Bicycle, Bus, Truck, Taxi, MotorScooter, Multimodal
		default "Auto"	
		
	optional enum collapse_geometry
		"Method used to collapse line and polygon sources and targets to a single point or a set of points that allow route
		finding."
		values Centroid
		default "Centroid"		

	optional text server
		"Specify the endpoint for the Valhalla server."
	
//	TODO configuration to use 

	class org.integratedmodelling.klab.components.network.services.RoutingRelationshipInstantiator

}



export object communities 
	"Detects communities/modules of a complex network using Infomap method."
{
	optional import object network
		"Semantics for the target network"
		default ""

	class org.integratedmodelling.klab.components.network.services.CommunityInstantiator

}


