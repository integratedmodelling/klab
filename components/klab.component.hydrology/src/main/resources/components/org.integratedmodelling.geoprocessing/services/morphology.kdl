/**
 * Interface contextualizers for the morphometry functionalities in JGrasstools / Horton machine.
 */
@klab 0.10.0
@version 0.10.0
@namespace im.geomorphology

export number classifier
	"Classifies each cell into the numeric category of the containing geological form using 
	 the Geomorphon algorithm."
	 label 'Classify terrain morphology'
{
	import number elevation
		"A DEM observation to classify"
		
	optional parameter number threshold 
		"Flatness threshold in meters. If unspecified it is chosen to match the radius in a square 
		 context." 
		default 0.0
		
	optional parameter number radius
		"Search radius in m. Represents the scaling of the point of view; large values can be used to
		 mimic far points of view or for very flat areas. If unspecified, it is set to 1/100th of the
		 diagonal of the bounding box."
		default 0.0
		
	class org.integratedmodelling.geoprocessing.morphology.GeomorphonResolver
}

//@constraint(xor(surface, chm))
export object findmaxima
	"Find relative maxima in a surface and extract them as points of polygons according to semantics. 
	 Optionally parameterizable for tree extraction with vegetation-specific options. Will optionally
	 produce a second artifact for the alternative geometry (points if the observable is areal) if
	 the model has a corresponding second observable of the expected dimensionality."
	 label 'Extract maxima'
{
	optional import number surface
		"A surface to scan for local maxima."
		
	optional import number chm
		"A crown height grid state (pre-differentiated with minimum values starting at 0)."
		
	optional number threshold 
		"Height threshold of relevance for any maximum found. If surface is passed, refers to the value in
		 the map and will be subtracted the minimum value. This parameter also accepts an expression where
		 the variable min and max will be substituted by the known minima and maxima in the surface." 
		default 0.0
		
	optional number relative-threshold 
		"Height threshold of relevance for any maximum found, expressed as a proportion of the maximum 
		 elevation over the baseline. If this is used, the 'threshold' parameter is used as a minimum
		 height, to cut off local maxima that may be too low." 
		default 0.0
			
	optional parameter number radius
		"Search radius in m. Represents the scaling of the point of view; large values can be used to
		 mimic far points of view or for very flat areas. If unspecified, it is set to 1/100th of the
		 diagonal of the bounding box."
		default 0.0
		
	optional enum mode
		"The mode of extraction. The default 'custom' is for general purposes and should be used for anything
		 but trees. If trees are being extracted, the modes 'conifer', 'deciduous' and 'mixed' will apply 
		 specific heuristics to improve detection."
		values custom, conifer, deciduous, mixed
		default "custom"
		
	class org.integratedmodelling.geoprocessing.morphology.MaximaFinderInstantiator
}

export number aspect
    "Estimates the aspect (i.e. the inclination angle of the gradient)
     by considering a reference system which puts the zero towards the
     north and the rotation angle anticlockwise. Hence, aspect is 0 in 
     the North direction and increases clockwise."
    label 'Aspect'
{
    import number elevation
        "The digital elevation model."

    geometry S2

    class org.integratedmodelling.geoprocessing.morphology.AspectResolver
}

export number gradient
    "Estimates the gradient in each site, defined as the module of the gradient vector."
    label 'Gradient'
{
    import number elevation
        "The digital elevation model."

    geometry S2

    class org.integratedmodelling.geoprocessing.morphology.GradientResolver
}